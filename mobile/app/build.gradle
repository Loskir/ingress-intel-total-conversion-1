apply plugin: 'com.android.application'

final BUILD_DATE = new Date().format('yyyy.MM.dd-HH:mm')

def getVersionCode = { ->
    def git = 'git rev-list HEAD --count'

    def proc = git.execute()
    proc.waitFor()
    def desc = "${proc.in.text}".trim()

    if (desc.isEmpty()) {
        println "ERROR setting version code"
        return -1
    }
    return Integer.parseInt(desc)
}

android {
    compileSdkVersion 28
    buildToolsVersion = '28.0.3'

    defaultConfig {
        applicationId "org.exarhteam.iitc_mobile"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode = getVersionCode()
        versionName "0.30.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "IITC_CRADLE_URL", "\"https://github.com/leCradle/\""
        buildConfigField "String", "IITC_FKLOFT_URL", "\"https://github.com/fkloft/\""
        buildConfigField "String", "IITC_GIUSEPPE_LUCIDO_URL", "\"http://be.net/giuseppelucido\""
        buildConfigField "String", "IITC_TG_NEWS_URL", "\"https://t.me/iitc_news\""
        buildConfigField "String", "IITC_TG_GROUP_URL", "\"https://t.me/iitc_group\""
        buildConfigField "String", "IITC_REDDIT_URL", "\"https://www.reddit.com/r/IITC/\""
        buildConfigField "String", "IITC_BUGTRACKER_URL", "\"https://github.com/IITC-CE/ingress-intel-total-conversion/issues/new\""
        buildConfigField "String", "IITC_WEBSITE_URL", "\"https://iitc.app/\""
        buildConfigField "String", "IITC_GITHUB_URL", "\"https://github.com/IITC-CE/ingress-intel-total-conversion\""
    }
    signingConfigs {
        debug {
            storeFile file("../keystore/debug.keystore")
        }
        release {
            if (System.getenv("TRAVIS")) {
                storeFile file("../keystore/android.jks")
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            } else {
                // We use keystore.properties file for signing data.
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-test-$BUILD_DATE"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    applicationVariants.all { variant ->
        variant.resValue  "string", "application_id", variant.applicationId
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'org.apache.commons:commons-text:1.8'
}

task copyUserscripts(type: Copy) {
    description = 'Updating prebuilt plugins'
    from '../assets'
    into 'src/main/assets'
    include('*')
    include('**/*')
}

preBuild.dependsOn copyUserscripts
